
HOWTO change the set of components built by default:
----------------------------------------------------

 The set of components to be built by default is determined by the default
 values of the respective configure command line argumnents.  

 Below is a shgortened version of saga_base.m4, focusing on the macro names, and
 the command line argument specs.  

 Arguments and their defaults, such as --enable-debug, are spefied like this:

   AC_ARG_ENABLE([argument-name],
                 [help-string],
                 [action-if-set],
                 [action-if-not-set])

 Thus, the fourth argument can be changed to reflect decent default values.
 Note that the help string should also reflect the defaults!


 Another argument specification, like --with-bindings=all, are specified just as
 above, as

   AC_ARG_WITH([argument-name],
               [help-string],
               [action-if-set],
               [action-if-not-set])

 Note though that the use can set special values, like 'all', which need to be
 documented in the help string, and need to be evaluated aftre the macros get
 called (see below).
   
==================================================================================

AC_DEFUN([AX_SAGA_CHECK_BASE],
[
  AC_ARG_ENABLE([debug],
                [AS_HELP_STRING([--enable-debug],
                [build debug version w/o optimization (default: yes)])],
                [],
                [enable_debug=yes])
])


AC_DEFUN([AX_SAGA_CHECK_BINDINGS],
[
  AC_ARG_WITH([bindings],
              AS_HELP_STRING([--with-bindings=all],
                             [comma separated list of SAGA language bindings to build.
                              possible values: C, Python, all (default is all).]),
              [
                if test "$withval" = "no"; then
                  want_bindings=""
                elif test "$withval" = "yes"; then
                  want_bindings="all"
                else
                  want_bindings=$withval
                fi
              ],
              [want_bindings="all"])

  if test "x$want_bindings" = "xall"; then
    want_bindings="C,Python"
  fi
])


AC_DEFUN([AX_SAGA_CHECK_PACKAGES],
[
  AC_ARG_WITH([packages],
              AS_HELP_STRING([--with-packages=all],
                             [comma separated list of SAGA API packages to build.
                              possible values: job, namespace, file, logicalfile, 
                              stream, rpc, advert, sd, message_bus, all (default is all).]),
              [
                if test "$withval" = "no"; then
                  want_packages=""
                elif test "$withval" = "yes"; then
                  want_packages="all"
                else
                  want_packages=$withval
                fi
              ],
              [want_packages="all"])

  if test "x$want_packages" = "xall"; then
    want_packages="job,namespace,file,logicalfile,stream,rpc,advert"
  fi
])

AC_DEFUN([AX_SAGA_CHECK_ADAPTORS],
[
  AC_ARG_WITH([adaptors],
              AS_HELP_STRING([--with-adaptors=all],
                             [comma separated list of SAGA adaptors to build.
                              possible values: job, file, logicalfile, stream, 
                              rpc, advert, sd, message_bus, all (default is all).]),
              [
                if test "$withval" = "no"; then
                  want_adaptors=""
                elif test "$withval" = "yes"; then
                  want_adaptors="all"
                else
                  want_adaptors=$withval
                fi
              ],
              [want_adaptors="all"])

  if test "x$want_adaptors" = "xall"; then
  # want_adaptors="job,namespace,file,logicalfile,stream,rpc,advert,sd,message_bus"
    want_adaptors="job,namespace,file,logicalfile,stream,rpc,advert,sd"
  fi
])


